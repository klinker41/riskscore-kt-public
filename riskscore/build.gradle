apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

test {
    useJUnitPlatform()
}

def commons_math_version = "3.6.1"

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.apache.commons:commons-math3:$commons_math_version"
    implementation project(':kalman1d')

    testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-property-jvm:$kotest_version"
    testImplementation project(':support')
}

kotlin {
    explicitApi()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
    }
}

shadowJar {
    classifier = null
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/nhsx/riskscore-kt-public")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("PACKAGES_ACCESS_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("PACKAGES_ACCESS_TOKEN")
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            from project.shadow.component(publication)
            groupId = "uk.nhs.covid19"
            version = "5.0.0"
            artifactId = 'riskscore-kt'
        }
    }
}
